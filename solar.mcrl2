% --------------------
% Sort declarations
% --------------------
sort 
	sPedalState = struct Pressed | Released;
	sDNRPState = struct D | R | NP;
	sCarState = struct SwitchOn | SwitchOff;
	sBatteryState = struct Unsafe | Safe | Full;
	sCCThreshold = struct BelowCCmin | AboveCCmin;
	sTurnOffThreshold = struct BelowTurnOffMax | AboveTurnOffMax;
	sDirection = struct Forwards | Backwards | Static;
	sCCState = struct Enable | Disable;
	sVirtualCarState = struct On | Off | CC;

% --------------------
% Action declarations
% --------------------
act
	% Input actions from the user and environment
  	aGasPeddal : sPedalState;
  	aBrakePeddal : sPedalState;
  	aDNRP : sDNRPState;
  	aCar : sCarState;
  	aBattery : sBatteryState;
  	aSpeed : sCCThreshold # sTurnOffThreshold # sDirection;
  	aCC : sCCState;

  	% Output actions
  	aVCS : sVirtualCarState;
  	aMotororce : sDirection;

% --------------------
% Communicating action declarations
% --------------------
act 
	% Used for communication from safety to CC
	recv_s2c_VCS,
	send_s2c_VCS, 
	s2c_VCS, 				
	% Used for communication from CC to safety		
	recv_c2s_VCS, 
	send_c2s_VCS, 
	c2s_VCS : sVirtualCarState;				

% --------------------
% Safety Procedure
% --------------------
proc Safety(vcs:sVirtualCarState, target:sVirtualCarState) =
	% Handle state changes
	(vcs != target) ->
		send_s2c_VCS(target).aVCS(target).Safety(target, target)
	
	+

	% Car button interactions
	sum btn:sCarState.aCar(btn).
		% The car is turned on
		(btn == SwitchOn && vcs==Off) ->
			Safety(vcs, On)
		<> 
		% The car is turned off
		(btn == SwitchOff && (vcs==On || vcs==CC)) ->
			Safety(vcs, Off)
		<> 
		% We don't have to change anything
		Safety(vcs, target)
	
	+
	
	% Battery is unsafe
	aBattery(Unsafe).
		Safety(vcs, Off)

	+
	
	% Cruise control interactions
	sum recv:sVirtualCarState.recv_c2s_VCS(recv).
		% Cruise control components requests to turn on CC
		(recv==CC && vcs==On) ->
			Safety(vcs, recv)
		<> 
		% Cruise control component requests to turn off CC
		(recv==On && vcs==CC) ->
			Safety(vcs, recv)
		<>
		% Request has been denied, no change applied but the unchanged state is send back
			Safety(vcs, vcs);


% --------------------
% Cruise control procedure
% --------------------
proc CruiseControl(vcs:sVirtualCarState) = 
	sum btn:sCCState.aCC(btn).
		(btn == Enable && vcs==On) ->
			send_c2s_VCS(CC).CruiseControl(vcs)
		<>
		(btn == Disable && vcs==CC) ->
			send_c2s_VCS(On).CruiseControl(vcs)
			
		<>
		CruiseControl(vcs)
	
	+

	sum newState:sVirtualCarState.recv_s2c_VCS(newState).CruiseControl(newState);
	%TODO add additional checks on conditions that can disable CC

% --------------------
% Drive procedure
% --------------------


% --------------------
% Process definition
% --------------------
init
	allow({ 
		aCar, aBattery, aCC, aVCS, s2c_VCS, c2s_VCS 
	},
	comm({ 
		recv_s2c_VCS | send_s2c_VCS -> s2c_VCS, 		% Communicates VCS from safety to CC
		recv_c2s_VCS | send_c2s_VCS -> c2s_VCS		% Communicates VCS from CC to safety
	},
	Safety(Off, Off) || CruiseControl(Off)
	));
