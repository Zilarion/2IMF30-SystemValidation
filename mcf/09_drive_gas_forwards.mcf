% Requirement 9
% After a DNRP(D) action occured after which a GasPedal(Pressed) occured there should be a path in which one of MotorForce(ForwarinD), DNRP(R), DNRP(NP), BrakePedal(Pressed) or Battery(Unsafe) occurs in a finite amount of steps.

nu X (inD:Bool = false, bp:Bool = false, battFull:Bool = false, carOn:Bool = false, rightDir:Bool = false).(
	% DNRP changes
	[aDNRP(D)] X(true, bp, battFull, carOn, rightDir) && 
	[aDNRP(R) || aDNRP(NP)] X(false, bp, battFull, carOn, rightDir) &&

	% Brake pedal changes
	[aBrakePedal(Pressed)] X(inD, true, battFull, carOn, rightDir) &&
	[aBrakePedal(Released)] X(inD, false, battFull, carOn, rightDir) &&

	% Battery changes
	[aBattery(Safe) || aBattery(Unsafe)] X(inD, bp, false, carOn, rightDir) &&
	[aBattery(Full)] X(inD, bp, true, carOn, rightDir) &&

	% Direction changes
	[forall c:sCCThreshold.forall o:sTurnOffThreshold.
	aSpeed(c, o, Forwards)] X(inD, bp, battFull, carOn, true) &&
	[forall c:sCCThreshold.forall o:sTurnOffThreshold.
	aSpeed(c, o, Static)] X(inD, bp, battFull, carOn, true) &&
	[forall c:sCCThreshold.forall o:sTurnOffThreshold.
	aSpeed(c, o, Backwards)] X(inD, bp, battFull, carOn, false) &&
	
	% CarOn Changes
	[aVCS(On)] X(inD, bp, battFull, true, rightDir) &&
	[aVCS(Off)] X(inD, bp, battFull, false, rightDir) &&

	% Other actions do not change the state of X
	[!(
	aDNRP(D) || aDNRP(R) || aDNRP(NP) ||
	aBrakePedal(Pressed) || aBrakePedal(Released) ||
	aBattery(Unsafe) || aBattery(Safe) || aBattery(Full) ||
	forall c:sCCThreshold.forall o:sTurnOffThreshold.aSpeed(c, o, Forwards) ||
	forall c:sCCThreshold.forall o:sTurnOffThreshold.aSpeed(c, o, Backwards) ||
	forall c:sCCThreshold.forall o:sTurnOffThreshold.aSpeed(c, o, Static) ||
	aVCS(On) || aVCS(Off)
	)] X(inD, bp, battFull, carOn, rightDir) && 

	% Gas pedal is pressed after some finite amount of time, we should see an aMotorForce(ApplyForceForwards) action
	% We use the fact here that the system does not need an arbitrary finite amount of steps, but will need exactly 3.
	(
	(!(val(inD) && !val(bp) && val(carOn) && ( (!val(battFull)) || val(rightDir)))) ||
	(
		% 3 steps after a aGasPedal(Pressed) it is possible to do a aMotorForce(ApplyForceForwards)
		[aGasPedal(Pressed)][true.true.true]<aMotorForce(ApplyForceForwards)> true &&
		% 3 steps after a sGasPedal(Pressed) no other action is possible
		[aGasPedal(Pressed)][true.true.true]<!aMotorForce(ApplyForceForwards)> false
	)
	)
)
