% Requirement 9
% After a DNRP(D) action occured after which a GasPedal(Pressed) occured there should be a path in which one of MotorForce(Forwards), DNRP(R), DNRP(NP), BrakePedal(Pressed) or Battery(Unsafe) occurs in a finite amount of steps.

nu X (ds:Bool = false, bp:Bool = false, bs:Bool = false).(
	% DNRP changes
	[c_DNRP(D)] X(true, bp, bs) && 
	[c_DNRP(R) || c_DNRP(NP)] X(false, bp, bs) &&

	% Brake pedal changes
	[c_BrakePedal(Pressed)] X(ds, true, bs) &&
	[c_BrakePedal(Released)] X(ds, false, bs) &&

	% Battery changes
	[c_Battery(Unsafe)] X(ds, bp, false) &&
	[c_Battery(Safe) || c_Battery(Full)] X(ds, bp, true) &&

	[!(exists d:sDNRPState . c_DNRP(d) || exists d:sPedalState . c_BrakePedal(d) || exists d:sBatteryState . c_Battery(d))] X(ds, bp, bs) && 

	% Gas pedal is pressed after some finite amount of time, we should see an aMotorForce(Forwards) action
	[aGasPedal(Pressed)] mu Y (dsL:Bool = ds, bpL:Bool = bp, bsL:Bool = bs) . (
		% DNRP changes
		[c_DNRP(D)] Y(true, bpL, bsL) && 
		[c_DNRP(R) || c_DNRP(NP)] Y(false, bpL, bsL) &&

		% Brake pedal changes
		[c_BrakePedal(Pressed)] Y(dsL, true, bsL) &&
		[c_BrakePedal(Released)] Y(dsL, false, bsL) &&

		% Battery changes
		[c_Battery(Unsafe)] Y(dsL, bpL, false) &&
		[c_Battery(Safe) || c_Battery(Full)] Y(dsL, bpL, true) &&

		([!aMotorForce(Forwards)] Y(dsL, bpL, bsL) && <true>true) && val(dsL && !bpL && bsL)
	)
)